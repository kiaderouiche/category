FSModelName = "@CLASSNAME@";
FSEigenstates = SARAH`EWSB;
AutomaticInputAtMSUSY = False;
FSDefaultSARAHModel = "HGTHDM-II";

(* input parameters *)

MINPAR = {
    {3, TanBeta}
};

EXTPAR = {
    {0, MSUSY},
    {1, MEWSB},
    {2, MuInput},
    {3, M1Input},
    {4, M2Input},
    {5, M3Input},
    {6, MAInput},
    {7, AtInput},
    {8, AbInput},
    {9, AtauInput},
    {100, LambdaLoopOrder}
};

EWSBOutputParameters = { M112, M222 };

(* The high scale where we match to the MSSM *)
HighScale = MSUSY;

HighScaleFirstGuess = MSUSY;

HighScaleInput = {
    {g1d    , g2          },
    {g1dp   , GUTNormalization[g1] g1 (* gY *)},
    {g2u    , g2          },
    {g2up   , GUTNormalization[g1] g1 (* gY *)},
    {Lambda1, 1/2 (1/4 ( (GUTNormalization[g1] g1)^2 + g2^2)
                   + UnitStep[LambdaLoopOrder-1] (deltaLambda1th1L + deltaLambda1Phi1L)
                   + UnitStep[LambdaLoopOrder-2] deltaLambda1th2L)},
    {Lambda2, 1/2 (1/4 ( (GUTNormalization[g1] g1)^2 + g2^2)
                   + UnitStep[LambdaLoopOrder-1] (deltaLambda2th1L + deltaLambda2Phi1L)
                   + UnitStep[LambdaLoopOrder-2] deltaLambda2th2L)},
    {Lambda3, 1/4 (-(GUTNormalization[g1] g1)^2 + g2^2)
              + UnitStep[LambdaLoopOrder-1] (deltaLambda3th1L + deltaLambda3Phi1L)
              + UnitStep[LambdaLoopOrder-2] deltaLambda3th2L},
    {Lambda4, -1/2 g2^2
              + UnitStep[LambdaLoopOrder-1] (deltaLambda4th1L + deltaLambda4Phi1L)
              + UnitStep[LambdaLoopOrder-2] deltaLambda4th2L},
    {Lambda5, 0
              + UnitStep[LambdaLoopOrder-1] (deltaLambda5th1L + deltaLambda5Phi1L)
              + UnitStep[LambdaLoopOrder-2] deltaLambda5th2L},
    {Lambda6, 0
              + UnitStep[LambdaLoopOrder-1] (deltaLambda6th1L + deltaLambda6Phi1L)
              + UnitStep[LambdaLoopOrder-2] deltaLambda6th2L},
    {Lambda7, 0
              + UnitStep[LambdaLoopOrder-1] (deltaLambda7th1L + deltaLambda7Phi1L)
              + UnitStep[LambdaLoopOrder-2] deltaLambda7th2L}
};

(* The scale where we impose the EWSB conditions
   and calculate the spectrum *)
SUSYScale = MEWSB;

SUSYScaleFirstGuess = MEWSB;

SUSYScaleInput = {
    {\[Mu]  , MuInput },
    {MassB  , M1Input },
    {MassWB , M2Input },
    {MassG  , M3Input },
    {M122   , MAInput^2 Sin[ArcTan[v2/v1]] Cos[ArcTan[v2/v1]]}
};

LowScale = LowEnergyConstant[MT];

LowScaleFirstGuess = LowEnergyConstant[MT];

LowScaleInput = {
   {Yu, Automatic},
   {Yd, Automatic},
   {Ye, Automatic},
   {v1, 2 MZMSbar / Sqrt[GUTNormalization[g1]^2 g1^2 + g2^2] Cos[ArcTan[TanBeta]]},
   {v2, 2 MZMSbar / Sqrt[GUTNormalization[g1]^2 g1^2 + g2^2] Sin[ArcTan[TanBeta]]}
};

InitialGuessAtLowScale = {
   {v1, LowEnergyConstant[vev] Cos[ArcTan[TanBeta]]},
   {v2, LowEnergyConstant[vev] Sin[ArcTan[TanBeta]]},
   {Yu, Automatic},
   {Yd, Automatic},
   {Ye, Automatic},
   {\[Mu]  , MuInput },
   {MassB  , M1Input },
   {MassWB , M2Input },
   {MassG  , M3Input },
   {M122   , MAInput^2 Sin[ArcTan[TanBeta]] Cos[ArcTan[TanBeta]]}
};

DefaultPoleMassPrecision = MediumPrecision;
HighPoleMassPrecision    = {hh};
MediumPoleMassPrecision  = {};
LowPoleMassPrecision     = {};

(* abbreviations *)
At   = AtInput;
Ab   = AbInput;
Atau = AtauInput;
Lambda1WagnerLee = 2 Lambda1;
Lambda2WagnerLee = 2 Lambda2;

(* arxiv:1508.00576, Eq. (45) *)
deltaLambda1th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY
    },
    (
        - kappa/2 ht^4 muMS^4
        + 6 kappa hb^4 AbMS^2 (1 - AbMS^2/12)
        + 2 kappa htau^4 AtauMS^2 (1 - AtauMS^2/12)
        + kappa (g2^2 + gY^2)/4 (3 ht^2 muMS^2 - 3 hb^2 AbMS^2 - htau^2 AtauMS^2)
    )
];

(* arxiv:1508.00576, Eq. (46) *)
deltaLambda2th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        6 kappa ht^4 AtMS^2 (1 - AtMS^2/12)
        - kappa/2 hb^4 muMS^4
        - kappa/6 htau^4 muMS^4
        - kappa (g2^2 + gY^2)/4 (3 ht^2 AtMS^2 - 3 hb^2 muMS^2 - htau^2 muMS^2)
    )
];

(* arxiv:1508.00576, Eq. (47)-(48) *)
deltaLambda3th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        kappa/6 muMS^2 (3 ht^4 (3 - AtMS^2)
                        + 3 hb^4 (3 - AbMS^2)
                        + htau^4 (3 - AtauMS^2))
        + kappa/2 ht^2 hb^2 (3 (AtMS + AbMS)^2
                             - (muMS^2 - AtMS AbMS)^2
                             - 6 muMS^2)
        - kappa/2 (g2^2 - gY^2)/4 (3 ht^2 (AtMS^2 - muMS^2)
                                   + 3 hb^2 (AbMS^2 - muMS^2)
                                   + htau^2 (AtauMS^2 - muMS^2))
    )
];

(* arxiv:1508.00576, Eq. (49) *)
deltaLambda4th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        kappa/6 muMS^2 (3 ht^4 (3 - AtMS^2)
                        + 3 hb^4 (3 - AbMS^2)
                        + htau^4 (3 - AtauMS^2))
        - kappa/2 ht^2 hb^2 (3 (AtMS + AbMS)^2
                             - (muMS^2 - AtMS AbMS)^2
                             - 6 muMS^2)
        + kappa/2 g2^2/2 (3 ht^2 (AtMS^2 - muMS^2)
                          + 3 hb^2 (AbMS^2 - muMS^2)
                          + htau^2 (AtauMS^2 - muMS^2))
    )
];

(* arxiv:1508.00576, Eq. (50) *)
deltaLambda5th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        - kappa/6 muMS^2 (3 ht^4 AtMS^2 + 3 hb^4 AbMS^2 + htau^4 AtauMS^2)
    )
];

(* arxiv:1508.00576, Eq. (51) *)
deltaLambda6th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY,
    gbar = ((GUTNormalization[g1] g1)^2 + g2^2) / 4
    },
    (
        kappa/6 muMS (+ 3 ht^4 muMS^2 AtMS
                      + 3 hb^4 AbMS (AbMS^2 - 6)
                      + htau^4 AtauMS (AtauMS^2 - 6))
        (* arxiv:hep-ph/9307201, Eq. (6.13)-(6.14) *)
        + gbar/2 kappa muMS (+ 3 AbMS hb^2
                             - 3 AtMS ht^2
                             + AtauMS htau^2)
    )
];

(* arxiv:1508.00576, Eq. (52) *)
deltaLambda7th1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY,
    gbar = ((GUTNormalization[g1] g1)^2 + g2^2) / 4
    },
    (
        kappa/6 muMS (+ 3 ht^4 AtMS (AtMS^2 - 6)
                      + 3 hb^4 muMS^2 AbMS
                      + htau^4 muMS^2 AtauMS)
        (* arxiv:hep-ph/9307201, Eq. (6.13)-(6.14) *)
        - gbar/2 kappa muMS (+ 3 AbMS hb^2
                             - 3 AtMS ht^2
                             + AtauMS htau^2)
    )
];

(* arxiv:1508.00576, Eq. (53) *)
deltaLambda1Phi1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY
    },
    (
        -kappa/6 (g2^2 + gY^2)/2 (3 ht^2 muMS^2 + 3hb^2 AbMS^2 + htau^2 AtauMS^2)
    )
];

(* arxiv:1508.00576, Eq. (54) *)
deltaLambda2Phi1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AtMS = At / MSUSY
    },
    (
        -kappa/6 (g2^2 + gY^2)/2 (3 ht^2 AtMS^2 + 3 hb^2 muMS^2 + htau^2 muMS^2)
    )
];

(* arxiv:1508.00576, Eq. (55) *)
deltaLambda3Phi1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    gY = GUTNormalization[g1] g1,
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        -kappa/6 (g2^2 - gY^2)/4 (3 ht^2 (AtMS^2 + muMS^2) + 3 hb^2 (AbMS^2 + muMS^2) + htau^2 (AtauMS^2 + muMS^2))
    )
];

(* arxiv:1508.00576, Eq. (56) *)
deltaLambda4Phi1L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    hb = Yd[3,3],
    htau = Ye[3,3],
    muMS = \[Mu] / MSUSY,
    AbMS = Ab / MSUSY,
    AtauMS = Atau / MSUSY,
    AtMS = At / MSUSY
    },
    (
        kappa/6 g2^2/2 (3 ht^2 (AtMS^2 + muMS^2) + 3 hb^2 (AbMS^2 + muMS^2) + htau^2 (AtauMS^2 + muMS^2))
    )
];

(* arxiv:1508.00576, Eq. (57) *)
deltaLambda5Phi1L = 0;
deltaLambda6Phi1L = 0; (* wrong in arxiv:hep-ph/9307201, Eq. (6.17) *)
deltaLambda7Phi1L = 0; (* wrong in arxiv:hep-ph/9307201, Eq. (6.17) *)

(* arxiv:1508.00576, Eq. (59) *)
deltaLambda1th2L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    muMS = \[Mu] / MSUSY
    },
    (
        -4/3 kappa^2 ht^4 g3^2 muMS^4
    )
];

(* arxiv:1508.00576, Eq. (60) *)
deltaLambda2th2L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    muMS = \[Mu] / MSUSY,
    AtMS = At / MSUSY
    },
    (
        16 kappa^2 ht^4 g3^2 (-2 AtMS + 1/3 AtMS^3 - 1/12 AtMS^4)
    )
];

(* arxiv:1508.00576, Eq. (61) *)
deltaLambda3th2L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    muMS = \[Mu] / MSUSY,
    AtMS = At / MSUSY
    },
    (
        2 kappa^2 ht^4 g3^2 AtMS muMS^2 (1 - 1/2 AtMS)
    )
];

deltaLambda4th2L = deltaLambda3th2L;
deltaLambda5th2L = 0;

(* arxiv:1508.00576, Eq. (62) *)
deltaLambda6th2L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    muMS = \[Mu] / MSUSY,
    AtMS = At / MSUSY
    },
    (
        4/3 kappa^2 ht^4 g3^2 muMS^3 (-1 + AtMS)
    )
];

(* arxiv:1508.00576, Eq. (63) *)
deltaLambda7th2L = With[{
    kappa = 1/(4 Pi)^2,
    ht = Yu[3,3],
    muMS = \[Mu] / MSUSY,
    AtMS = At / MSUSY
    },
    (
        4 kappa^2 ht^4 g3^2 muMS (2 - AtMS^2 + 1/3 AtMS^3)
    )
];
